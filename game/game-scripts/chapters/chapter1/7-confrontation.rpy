label confrontation_path:

        # Неон и Алекс выходят от поста охраны
        scene bg chapter_1_lab_corridor_3 with fade
        play music "music/BGM/Promising.ogg" fadeout 5.0
        # play music "sounds/determination_theme.ogg" loop fadein 1.0
        show alex happy at right with easeinright
        show neon proud at center with easeinright

        alex "Ну что, теперь к Бауманну? С этим видео его не отвертеться."
        neon "Да. Грубенманну я больше не верю. Только к генеральному."

        narrator """
        Они быстрым шагом направились к лифтам, ведущим в административное крыло.

        Когда двери лифта открылись, они столкнулись лицом к лицу с Маркусом. Он как раз выходил из кабины.

        Увидев их, он на мгновение замер. На его лице промелькнуло удивление, которое он тут же попытался скрыть за маской снисходительного спокойствия.
        """

        show alex concerned
        show neon neutral
        show marcus suit_happy at left with dissolve

        marcus " Неон. Алекс. Какая встреча. Решили прогуляться?"

        narrator """
        Неон не ответила. Она просто посмотрела на него. Холодным, тяжелым взглядом.
        
        В ее глазах не было ни сарказма, ни злости. Только ледяная решимость.
        """

        neon "Отойди с дороги, Маркус."

        narrator """
        Она сделала шаг вперед, намереваясь пройти мимо. Алекс последовала за ней.

        Маркус, немного удивился, не ожидая, что Неон назовёт его по официальному имени, но тут же выпрямился и преградил им путь. 
        
        Маркус не двинулся с места, преграждая им путь в лифт, опираясь на дверь своим телом. Его улыбка стала более натянутой.
        """

        marcus suit_smug "Куда это вы так спешите? К директору? С чем вы к нему пойдете?"

        neon "Узнаешь. У тебя же были дела? Ну так вот, иди."

        narrator "Видя, что они не собираются останавливаться, Маркус запаниковал. Его спокойствие испарилось."
        stop music fadeout 60.0
        marcus suit_panic """
        Думаешь, я не знаю, где вы были? 

        Думаешь, этот страый хрыч не позвонил мне сразу, как вы ушли?! 
        
        Думаешь, ваше видео что-то изменит?!
        """

        neon "..."

        narrator "Алекс подняла телефон, на экране которого была видна миниатюра записи. Этого было достаточно."

        marcus """
        Думаете, Бауманну есть дело до ваших мелких интрижек? 
        
        Его интересует результат! А результат сегодня представил я!
        """
        marcus @ suit_sinister """
        А ты... ты для них уже никто! 

        Грубенманн уже давно точит на тебя зуб!

        Я уже подготовил акт о твоей профнепригодности, и после сегодняшнего его подпишут не глядя!
        """

        narrator "Неон продолжала идти прямо на него, вынуждая его пятиться. Она не говорила ни слова, и это молчание пугало его больше, чем крики."

        marcus """
        
        А твое видео... кхм... оно просто исчезнет! Как и ты, если не остановишься!

        Да! Этот город маленький, Неон. 
        
        А горы... горы вокруг большие, потеряться здесь будет очень легко!
        
        """
        
        marcus @ suit_sinister "А теперь, удали запись и... больше не вспоминай о ней!"
        
        alex serious "Это угроза?! Мы сейчас же покажем это директору!"

        marcus @ suit_sinister "Заткнись! Я не с тобой говорю, сгинь!"
        

        narrator """
        
        Именно в этот момент, когда Неон была уже в шаге от него, готовая просто оттолкнуть его с пути, Маркус совершил роковую ошибку.

        Он перешел от угроз к личному оскорблению, пытаясь задеть ее за самое живое.
        """

        marcus suit_sinister "Да... Да кто тебя вообще слушать будет? По-посмотри на себя!"

        play ambient "ambient/Target.ogg" fadein 5.0

        marcus "Вечно как не от мира сего, {b}Синяя ворона{/b}! Таким, как ты, здесь не место!"
        
        
        show neon surprised
        narrator "Синяя ворона."
        
        # play sound "sounds/flashback_trigger_school.ogg"
        # Эффект: Короткая, резкая вспышка. На долю секунды на лице Маркуса проступает лицо задиры из флешбэка.
        # with Flash(1.0, 0.2, 1.0, color="#FFFFFF")

        hide alex with easeoutright
        narrator """
        Это слово. \n
        Это презрение. \n 
        Это было не про карьеру. \n
        Это было про нее саму. \n
        Это был голос из прошлого.
        
        Голос завуча. Голос опекунши. Голос тех, кто всегда говорил ей, что она 'не такая'

        Ледяное спокойствие треснуло. И из-под него вырвался раскаленный поток чистой, испепеляющей ярости.
        """
        show neon shadowed with Dissolve(2.0)
        neon "Что... ты... сказал?"
        narrator """Она остановилась. Ее взгляд, до этого холодный, загорелся безумным огнем.

        Кровь ударила в голову. Ярость, слепая и испепеляющая, затопила сознание.

        Она посмотрела на Маркуса уже не как на коллегу-предателя, а как на воплощение всего того, что ее мучило годами.

        Ее взгляд упал на тяжелые металлические стулья, стоящие вдоль стены.
        """
        show neon frustrated with Dissolve(2.0)
        neon "Мне... никогда... больше не будут... указывать какая я!"
        narrator "Она шагнула к одному из стульев. Алекс, увидев выражение ее лица, испуганно отшатнулась."
        show alex serious at right with easeinright
        alex "Неон... Стой! Не делай глупостей!"
        show neon frustrated_power with { "master" : Dissolve(5.0) }
        with { "effect": dissolve }
        narrator """
        Но Неон уже не слышала ее. Она схватилась за спинку стула.
        
        Он казался невероятно тяжелым, но ее ярость давала ей силы, которых она в себе не подозревала.

        С криком, вырвавшимся из глубины души, она оторвала стул от пола.
        """
        $renpy.music.set_volume(0.25, channel='ambient')
        
        play music "music/BGM/DramaQueen.ogg" fadein 10.0 volume 0.75
        # play sound "sounds/metal_scrape_loud.ogg"
        # play sound "sounds/neon_roar_effort.ogg" # Звук усилия/рыка Неон

        marcus suit_panic "Ты... Ты что, совсем рехнулась?! Положи стул!"

        narrator "Неон замахнулась, а её синие волосы светились как пламя..."
return