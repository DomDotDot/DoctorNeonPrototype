label ch3_hall_explore:
    scene bg chapter_3_start-mainhall with dissolve

    menu:
        "Осмотреться.":
            narrator """
            Я медленно обвела взглядом свое узилище. Главный холл встречал гнетущей тишиной.

            Белые потолки, когда-то ровные, теперь исполосованы неровной голубой полосой, словно след от неумелой попытки ремонта. Многочисленные трещины расползлись паутиной по кафельному полу, местами застыли грязные лужи от протекающей крыши.

            Стены были покрыты слоями грязи и уродливыми граффити. 'Туман забрал мою работу', 'Помни Аномик' – кричали надписи. Это место было не просто заброшено, оно было оплакано.

            Слева холл уходил в полумрак, откуда пробивался теплый, мерцающий свет костра.

            Прямо напротив меня виднелась комната, запертая большой металлической решеткой. Плотный слой пыльной простыни скрывал то, что находилось внутри.

            Правее решетки – приоткрытая дверь, залитая бледным дневным светом. Но путь преграждал массивный шкаф, накренившийся и плотно заблокировавший проход.

            Еще правее возвышалась лестница на второй этаж. Хлипкая оградка и зияющий пролом в перекрытии не внушали доверия.

            И, наконец, за спиной – тот самый выход и его живая стена. Вышибала.
            """
            $ hall_examined = True
            jump ch3_hall_explore
        
        "Осмотреть стойку регистрации." if hall_examined and not has_keycard:
            narrator """
            Я подошла к стойке. На полу, среди разбросанных бумаг и мусора, что-то блеснуло под слоем пыли.
            
            Я наклонилась и подняла находку. Ключ-карта. С фотографией улыбающегося мужчины средних лет. Кто-то бежал отсюда в такой спешке, что обронил свой пропуск.
            """
            $ has_keycard = True
            "Вы получили {b}Ключ-карту{/b}."
            jump ch3_hall_explore

        "Осмотреть ключ-карту." if has_keycard and not inspected_keycard_barcode:
            # Изображение карты можно показать на экране
            # show keycard_image
            narrator """
            Я внимательно рассмотрела карту.
            
            Имя: {b}Евгений Петров{/b}.
            Должность: {b}Старший химик{/b}.
            
            На обратной стороне был штрихкод. Под ним шла череда цифр, но что-то было не так.

            Некоторые вертикальные линии штрих-кода были нарочито жирными, выделяющимися на фоне остальных. Словно маркер, указывающий на цифры прямо под ними.
            """

            neon "{=thoughts}Жирные линии находятся над цифрами {b}1{/b}, {b}9{/b}, {b}8{/b} и {b}4{/b}. Остальные линии тонкие. Это не ошибка печати. Это шифр. Евгений Петров, старший химик...{/thoughts}"
            narrator "Я убрала карту в карман, запомнив эту странную последовательность."

            # hide keycard_image
            $ inspected_keycard_barcode = True
            $ found_code_clue = True
            jump ch3_hall_explore
        
        "Поговорить с вышибалой.":
            jump ch3_bouncer_talk_menu

        "Пройти в левую часть холла." if hall_examined:
            jump ch3_left_wing

        "Подойти к решетке в центре." if hall_examined:
            jump ch3_gate_menu
        
        "Подойти к забаррикадированной двери." if hall_examined:
            jump ch3_cabinet_door

        "Подойти к двери с электронным замком." if hall_examined:
            jump ch3_guts_office_door

        "Подняться по лестнице наверх." if hall_examined:
            jump ch3_stairs
