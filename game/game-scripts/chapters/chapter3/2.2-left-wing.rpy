# --- ЛОКАЦИИ ДЛЯ ИССЛЕДОВАНИЯ ---

label ch3_left_wing:
    scene black with fade
    narrator "Я осторожно подхожу к углу стены в левом крыле."

    if not tried_to_talk_to_thugs:
        narrator "Заглянув за угол, я вижу ту самую сцену: трое мужчин греются у огня в бочке. Они не замечают меня."
    show cg cg18 with dissolve
    menu ch3_left_wing_thug_talk:

        "Подслушать разговор." if tried_to_talk_to_thugs:
            $ thugs_listen_count += 1
            if thugs_listen_count < 5:
                narrator "Они продолжают свой разговор о какой-то ерунде. Ничего полезного для меня."
                jump ch3_left_wing_thug_talk
            elif thugs_listen_count == 5 and not knows_bouncer_secret:
                narrator "Я снова прислушиваюсь. На этот раз тон разговора сменился..."
                # play sound "sounds/thugs_muffled_conversation_secret.ogg"
                "Бандит 1" "...жалко мне его все-таки. Зверь, конечно, но... с дочкой такое."
                "Бандит 2" "Да уж, туман этот... не щадит никого. Говорят, она в коме. Он все деньги спускает на 'Сон', чтобы хоть как-то ее поддержать."
                "Бандит 1" "Потому и вкалывает на Гатса. Думает, накопит и свалит с ней куда-нибудь. Наивный."
                narrator """
                Дочь... в коме от тумана.
                
                Так вот в чем дело. Это его слабое место. Жестоко, но... это может стать моим ключом на свободу.
                """
                $ knows_bouncer_secret = True
                jump ch3_left_wing_thug_talk
            else:
                narrator "Я уже все слышала. Больше они ничего важного не скажут."
                jump ch3_left_wing_thug_talk
        
        "Попытаться заговорить с ними." if not tried_to_talk_to_thugs:
            # Этот вариант доступен, пока Неон не привлекла слишком много внимания
            narrator "Как только я делаю шаг к двери, намереваясь их окликнуть, их разговор становится громче. Я замираю за стеной, прислушиваясь... Их голоса доносились через щели в дверях."

            # play sound "sounds/thugs_muffled_conversation.ogg"
            "Бандит 1" "...и смотри, когда солнце поднимется выше, на улицу ни ногой. Босс сказал, сегодня туман будет злой."
            "Бандит 2" "Да знаю я. В прошлый раз надышался этой дрянью, потом неделю черным кашлял. Говорят, от него вообще крыша едет, если долго дышать."
            "Бандит 1" "Не то слово. Эта вонь... она тебя изнутри выжигает."
            "Бандит 2" "Эх, вот бы свалить отсюда... в Веритас-Сити. Говорят, там воздух чистый как слеза. И работа есть."
            "Бандит 1" """
            Ага, свалить. Ты хоть знаешь, что там творится? Там за украденную шоколадку пальцы ломают.
            
            Порядок, конечно, идеальный, но шаг влево, шаг вправо – и ты в тюрьме. Не для таких, как мы, короче.
            """
            neon "{=thoughts}Веритас-Сити... Город-академия. Чистый, безопасный, недостижимый. Другая вселенная. Я понимаю, что разговор с ними бесполезен."
            $ tried_to_talk_to_thugs = True

            hide cg cg18 with fade
            jump ch3_hall_explore

        "Вернуться в главный холл.":
            jump ch3_hall_explore