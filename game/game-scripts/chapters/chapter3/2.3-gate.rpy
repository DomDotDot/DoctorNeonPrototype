label ch3_gate_menu:
    # Пазл с воротами из V1
    narrator "Металлическая решетка холодно поблескивает. За ней – темнота, из которой доносятся странные шорохи."
    menu:
        "Осмотреть простыню.":
            narrator """
            Я любопытно отдергиваю простыню. Под ней обнаруживается сложный механизм из шестерен и валиков, покрытый толстым слоем пыли и ржавчины.
            
            Это была массивная сдвижная решетка из толстых железных прутьев. Она вела в другое крыло завода
            """
            $ inspected_gate_mechanism = True
            jump ch3_gate_menu

        "Попытаться открыть ворота." if inspected_gate_mechanism and not factory_gate_open:
            # play sound "sounds/metal_grate_struggle_fail.ogg" # Звук безуспешной попытки сдвинуть решетку
            narrator """
            Я ухватываюсь за створку и тяну. Раздается протяжный скрип металла, створка сдвигается на пару сантиметров и застревает.

            Зубья шестерен с трудом цепляются друг за друга. 
            
            Теперь понятно. Ворота не заперты на замок, их просто заклинило от времени. Этому механизму отчаянно нужна смазка.
            """
            jump ch3_gate_menu
        
        "Использовать канистру со смазкой." if inspected_gate_mechanism and has_motor_oil:
            # play sound "sounds/oil_can_pouring.ogg"
            narrator """
            Механизм расположен слишком высоко, просто так не дотянуться. Но прутья ворот могут послужить лестницей.
            
            Я аккуратно взбираюсь на нижние прутья, стараясь не шуметь. С этой высоты я легко дотягиваюсь до механизма.

            Но теперь у меня было решение. 
            
            Я открыла канистру и щедро полила густой, темной смазкой весь механизм – засов, направляющие, ролики.
            
            Смазка медленно, неохотно проникала в щели.
            
            Я спрыгиваю на пол и я снова уперлась в решетку плечом, навалившись всем весом.
            
            На этот раз она поддается.
            """
            # play sound "sounds/metal_grate_creak_start.ogg" # Звук: начало скрипа
            narrator """
            С оглушительным, протестующим визгом ржавчины, решетка медленно, по сантиметру, начала поддаваться. 
            
            Я сдвинула ее ровно настолько, чтобы можно было протиснуться в образовавшуюся щель, открывая проход в длинный, темный коридор.

            Сжав кулаки, я решительно шагаю вперед.
            """
            # play sound "sounds/metal_grate_screech_open_heavy.ogg" # Звук: тяжелый, громкий скрип открывающейся решетки
            neon "Я найду выход из этого проклятого места!"
            $ factory_gate_open = True
            jump ch3_long_corridor
            
        "Пройти в коридор." if factory_gate_open:
            jump ch3_long_corridor

        "Вернуться в холл.":
            jump ch3_hall_explore