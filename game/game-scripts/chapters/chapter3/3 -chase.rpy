label ch3_chase_start:
    # Проверка, чтобы игрок не попал сюда случайно
    if provocation_count < 3: # Увеличим порог до 3 провокаций
        narrator "Нужно еще что-то сделать, чтобы вывести его из себя."
        jump ch3_hall_explore

    narrator "Он бежал на меня, как разъяренный бык. Нужно было решать, куда бежать!"

    menu:
        "К выходу!":
            narrator """
            Я бросилась к выходу, срываясь с места и мчась к выходу, сердце колотится в груди. Каждый шаг отдает в висках, а в ушах шумит кровь.

            Я у двери, дрожащей рукой я хватаюсь за дверную ручку, нервно дергая её, наваливаюсь всем телом...
            """
            show neon at center with easeinleft
            show neon shadowed with Dissolve(5.0)
            neon "Заперто..."
            jump game_over_bouncer

        "К тому шкафчику, что баррикадой стоит у прохода.":
            narrator """
            Я рванула в угол, к шатающемуся шкафчику. Он несся за мной, его тяжелые шаги отдавались гулким эхом по всему холлу.
            
            В последний момент я резко присела, проскальзывая мимо шкафа.
            """
            # play sound "sounds/body_slide_floor.ogg"
            narrator "Вышибала, замахнувшись для удара, промахнулся. Его огромный кулак со всей силы врезался в дверцу шкафчика."
            # play sound "sounds/metal_punch_dent_door_bust.ogg"
            narrator "Раздался оглушительный треск металла. Дверца, не выдержав такого удара, отлетела в сторону."
            jump ch3_bouncer_success

        "На лестницу, ведущую на второй этаж.":

            narrator """
            Я бросилась к лестнице, ведущей на второй этаж. Сердце колотится в груди, адреналин зашкаливает.

            Вышибала несется за мной, его тяжелые шаги гулко отдаются по всему холлу.

            На лестнице в силу паники я спотыкаюсь и падаю, ударяясь о ступеньки и падаю.

            Вышибала поймал меня, но не для того, чтобы помочь встать...
            """

            jump game_over_bouncer

            


# --- ЗАВЕРШЕНИЕ СЦЕНЫ ---

label ch3_bouncer_success:
    "Вышибала" """
    Ну ладно... черт с тобой.

    Надеюсь, ты уяснила, что так делать не стоит.
    
    А иначе ты будешь как этот шкаф.
    """
    # play sound "sounds/kick_metal.ogg" with vpunch
    narrator "Он брезгливо пнул шкаф носком тяжелого ботинка и, бормоча ругательства, вернулся на свой пост у входа."
    
    $ locker_broken = True
    narrator "Он ушел. А я осталась стоять у разбитого шкафа, в котором теперь зияла дыра... и возможность."
    jump ch3_cabinet_door

label game_over_bouncer:
    stop music
    stop ambient
    scene black with dissolve
    "Вышибала" "Попалась." with vpunch
    pause(3.0)
    jump chapter_3_rpy
    return